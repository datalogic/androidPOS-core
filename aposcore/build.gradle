plugins {
    id 'com.android.library'
    id 'maven-publish'
}
group='com.github.datalogic'
def libVersion = "0.1-alpha"

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName libVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            if (outputFile != null && outputFileName.endsWith('.aar')) {
                outputFileName = "${archivesBaseName}-${variant.name}.aar"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    testImplementation 'junit:junit:4.+'
    testImplementation 'com.google.truth:truth:1.1.2'
    testImplementation 'org.mockito:mockito-core:3.10.0'
    testImplementation 'org.mockito:mockito-inline:3.10.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'com.google.truth:truth:1.1.2'
    implementation 'com.github.datalogic:androidpos-controls:v0.2-alpha'
    implementation 'com.github.datalogic:androidpos-commons:v0.2-alpha'
    implementation 'com.github.mik3y:usb-serial-for-android:3.4.3'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

publishing {
    publications {
        release(MavenPublication) {
            groupId group
            //You can either define these here or get them from project conf elsewhere
            artifactId 'androidpos-core'
            version libVersion
            artifact("$buildDir/outputs/aar/aposcore-release.aar")
            artifact sourcesJar
            //aar artifact you want to publish

            //generate pom nodes for dependencies
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each { dependency ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                    dependencyNode.appendNode('scope', 'implementation')
                }
            }
        }
    }

}